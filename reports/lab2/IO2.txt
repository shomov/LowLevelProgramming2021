T 56 K
P 0 D      [1]
P 1 F      [2]
XF
XF
XF
XF
XF
P 2 D       [количество элементов = 5]
P 2 F       [количество сдвигов = 4]
XF          [кол-во внутр сдвигов]

T 256 K
P1F         [2]
P2F         [4]
P3F         [6]
P4F         [8]
P5F         [10]


T 150 K     [подпрограмма удаления лишних кругов сдвига]
GK
A 3 F       [инструкция возврата]
T 8 @       [запись инструкции в конец подпрограммы]
A 64 F      [чтение числа перестановок]
S 63 F      [вычитание из числа перестановок числа элементов]
E 3 @       [если результат положительный - повторение]
A 63 F      [прибавление числа элементов - выведение в положительный диапазон]
T 64 F      [запись нового числа сдвигов - без лишних полных оборотов]
X 0 F
E 0 F

T 200 K     [подпрограмма сдвигов]
GK
A 3 F       [Инструкция возврата (формируется в аккумуляторе)]
T 37 @      [Записываем инструкцию возврата в конец]
A 19 @	    [чтение изначального варианта полуфункции команды каретки]
T 38 @      [сохранение команды]
A 20 @      [чтение изначального варианта полуфункции команды каретки]
T 39 @      [сохранение команды]
A 64 F      [НЦ сдвигов, чтение числа сдвигов]
S 56 F      [вычитание 1]
G 36 @      [если сдвиги выполнены - завершение]
T 64 F      [запись числа сдвигов]
A 260 F     [чтение последнего элемента массива]
T 41 @      [сохранение последнего элемента массива]
A 63 F      [берём в акк число элементов, равное числу внутренних переносов]
S 56 F      [вычитание 1]
T 40 @      [сохранение числа переносов]
A 40 @      [НЦ внутренних переносов]
S 56 F      [вычитание 1]
G 27 @      [если все переносы выполнены, то переход к постановке сохранённого последнего элемента в начало]
T 40 @      [сохранение числа переносов]
A 259 F     [функция "каретки" чтения предыдущего элемента]
T 260 F     [функция "каретки" записи предыдущего элемента на место нынешнего]
A 19 @      [чтение полукоманды "каретки"]
S 57 F      [сдвиг "каретки"]
T 19 @      [запись новой команды "каретки"]
A 20 @      [чтение полукоманды "каретки"]
S 57 F      [сдвиг "каретки"]
T 20 @      [запись новой команды "каретки"]
E 15 @      [КЦ внутренних переносов, возврат в начало цикла]
TF
A 41 @ 	    [финальное перемещение последнего элемента на место первого, чтение]
T 256 F	    [запись на первую позицию]
A 38 @	    [чтение изначальной команды "каретки"]
T 19 @	    [возврат изначальной версии]
A 39 @	    [чтение изначальной команды "каретки"]
T 20 @	    [возврат изначальной версии]
E 6 @ 	    [КЦ сдвигов, возврат в начало цикла]
TF
E 0 F
P0F         [хранение изначальной версии полуфункции "каретки"]
P0F         [хранение изначальной версии полуфункции "каретки"]
P0F         [кол-во переносов внутри массива]
P0F         [хранение последнего элемента]


T 100 K     [программа]
GK
Z 0 F
T 0 F
A 2 @
G 150 F     [вызов подпрограммы удаления кругов]
Z 0 F
T0F
A 2 @
G 200 F     [вызов подпрограммы сдвигов]
Z 0 F
EZPF



